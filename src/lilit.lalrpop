use std::str::FromStr;
use ast::*;

grammar;

pub Mod: Box<Mod> = {
  <u:ModUnit*> => Box::new(Mod { units:u }),
};

ModUnit: Box<ModUnit> = {
	<Class> => Box::new(ModUnit::Class(<>)),
	<Func> => Box::new(ModUnit::Func(<>)),
};

Class: Box<Class> = {
  "class" <n:Name> <e:ClassExtend?> <m:Func*> "end" => Box::new(Class {
    name:n,
    extends: match e {
    	None => vec![],
    	Some(e) => e,
    },
    methods:m,
  })
};

ClassExtend: Vec<String> = {
  "extends" <e:Name> <v:("," <Name>)*> => {
  	let mut v = v;
  	v.insert(0, e);
  	v
  }
};

Func: Box<Func> = {
  "def" <id:Id> "(" ")" <exprs:Expr*> "end" => Box::new(Func { id:id, exprs:exprs }),
};

Expr: Box<Expr> = {
  <Num>,
  <Invoke>,
  <Assignment>,
  <ReadVar>,
};

Assignment: Box<Expr> = {
  "val" <var:Var> "="  <expr:Expr> => Box::new(Expr::Assignment(Box::new(Assignment { var, expr })))
}

Invoke: Box<Expr> = {
  <Id> "(" ")" => Box::new(Expr::Invoke(Box::new(Invoke { id: <> }))),
};

ReadVar: Box<Expr> = {
  <Var> => Box::new(Expr::Var(<>))
}

Var: Box<Var> = {
  <Id> => Box::new(Var { id: <> })
}

Id: Box<Id> = {
  r"[a-z][a-z0-9_]*" => Box::new(Id { name: String::from(<>) }),
};

Name: String = {
  r"[A-Z][a-zA-Z0-9_]*" => String::from(<>),
};

Num: Box<Expr> = {
  r"[0-9]+" => Box::new(Expr::Num(Box::new(Num { value: i32::from_str(<>).unwrap() }))),
};
